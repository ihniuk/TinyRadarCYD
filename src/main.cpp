#include <Arduino.h>
#include <ArduinoJson.h>
#include <WiFiManager.h>
#include <cmath>

#include "LGFX.h"
#include "WiFiManagerHelpers.h"
#include "ConfigurationWebServer.h"
#include "HttpRequestManager.h"
#include "OpenSkyAuthTokenHandler.h"
#include "models/Aircraft.h"
#include "models/TrackedAircraft.h"
#include <map>

#define SCREEN_SIZE 240
#define SCREEN_SIZE_DIV_2 (SCREEN_SIZE / 2)

LGFX tft;
LGFX_Sprite backbuffer(&tft);

WiFiManager wm;
ConfigurationWebServer configServer;
HttpRequestManager http;
OpenSkyAuthTokenHandler authHandler(&http);

std::map<String, TrackedAircraft> trackedAircraft;

const unsigned long FETCH_INTERVAL = 21000;  // ms
unsigned long lastFetch = FETCH_INTERVAL;

void setup()
{
  Serial.begin(115200);
  // delay(1000); // avoids immediate serial output being cut off - uncomment if needed

  // initialise LGFX + screen
  tft.init();
  tft.invertDisplay(true);
  pinMode(3, OUTPUT);
  digitalWrite(3, HIGH);

  backbuffer.setColorDepth(8);
  backbuffer.createSprite(SCREEN_SIZE, SCREEN_SIZE);

  // establish WiFi connection
  tft.fillScreen(lgfx::color888(0, 0, 0));
  tft.setTextColor(lgfx::color888(0, 255, 0));
  tft.drawCentreString("Connecting to WiFi...", SCREEN_SIZE / 2, SCREEN_SIZE / 2);

  WiFiManagerHelpers::ConfigureWiFiManager(wm);
  wm.autoConnect(WiFiManagerHelpers::WiFiManagerName);

  // begin background server for configuration
  configServer.Initialise();
}

void loop()
{
  unsigned long now = millis();
  const double lat = 51.454863,
    lon = -0.320663,
    rad = 0.25;

  // fetch cycle
  if (now - lastFetch >= FETCH_INTERVAL) {
    lastFetch = now;

    // auth
    String token = authHandler.GetValidToken(
      configServer.GetStoredString("opensky-id"),
      configServer.GetStoredString("opensky-secret")
    );

    std::vector<std::pair<String, String>> headers = {};
    if (!token.isEmpty()) {
      Serial.println("Token available - adding auth header");
      headers.push_back({ "Authorization", "Bearer " + token });
    }

    // request
    String airplaneStateResponse = http.Get(
      "https://opensky-network.org/api/states/all",
      {
        {"lamin", String(lat - rad)},
        {"lamax", String(lat + rad)},
        {"lomin", String(lon - rad)},
        {"lomax", String(lon + rad)}
      },
      headers
    );

    // track
    JsonDocument doc;
    deserializeJson(doc, airplaneStateResponse);
    auto aircraft = JsonParser::ParseArray<Aircraft>(doc["states"]);

    for (auto& ac : aircraft) {
      trackedAircraft[ac.icao24] = { ac, now };
    }
  }

  // draw cycle
  backbuffer.fillScreen(lgfx::color888(0, 0, 0));
  backbuffer.drawCircle(SCREEN_SIZE_DIV_2, SCREEN_SIZE_DIV_2, SCREEN_SIZE_DIV_2 - 1, lgfx::color888(0, 255, 0));
  backbuffer.drawCircle(SCREEN_SIZE_DIV_2, SCREEN_SIZE_DIV_2, SCREEN_SIZE_DIV_2 / 2 - 1, lgfx::color888(0, 64, 0));
  backbuffer.fillCircle(SCREEN_SIZE_DIV_2, SCREEN_SIZE_DIV_2, 3, lgfx::color888(0, 32, 0));

  for (auto& [icao, tracked] : trackedAircraft) {
    if (tracked.state.onGround) continue;

    // predict for smooth motion between requests
    auto [predLat, predLon] = tracked.PredictPosition();

    // project longitude and latitude to screen coords
    float lonScale = cos(lat * DEG_TO_RAD);

    float dLon = (predLon - lon) * lonScale;
    float dLat = predLat - lat;

    float normLon = (dLon + rad) / (2.0f * rad);
    float normLat = (dLat + rad) / (2.0f * rad);

    int x = normLon * SCREEN_SIZE;
    int y = SCREEN_SIZE - (normLat * SCREEN_SIZE);

    // calculate triangle vertices based on travel direction
    float dx = std::sin(radians(tracked.state.trueTrack));
    float dy = -std::cos(radians(tracked.state.trueTrack));

    float px = -dy;
    float py = dx;

    float length = 6.0f;
    float width = 3.0f;

    float tipX = x + dx * length;
    float tipY = y + dy * length;

    float leftX = x - dx * length * 0.5f + px * width * 0.5f;
    float leftY = y - dy * length * 0.5f + py * width * 0.5f;

    float rightX = x - dx * length * 0.5f - px * width * 0.5f;
    float rightY = y - dy * length * 0.5f - py * width * 0.5f;

    // draw text
    backbuffer.setTextSize(1);
    backbuffer.setTextColor(lgfx::color888(0, 128, 0));
    backbuffer.drawString(tracked.state.callsign, x + 5, y + 5);
    backbuffer.drawString(String(tracked.state.velocity) + "m/s", x + 5, y + 5 + (tft.fontHeight() + 1));
    backbuffer.drawString(String(tracked.state.baroAltitude) + "m", x + 5, y + 5 + (tft.fontHeight() * 2 + 1));

    // draw representation of plane
    backbuffer.fillTriangle(tipX, tipY, leftX, leftY, rightX, rightY, lgfx::color888(0, 255, 0));
  }

  backbuffer.pushSprite(0, 0);
}